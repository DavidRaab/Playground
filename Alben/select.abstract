#!/usr/bin/env perl
use strict;
use warnings;
use v5.32;
use Data::Printer;
use DBI;
use SQL::Abstract;
use Text::Table;
use JSON;
use Getopt::Long;

# Argument Handling
my $search  = "%";
my $json    = 0;
my $verbose = 0;
GetOptions(
    "j|json"     => \$json,
    "s|search=s" => \$search,
);

# Database Connection and fetching data
my $dbh = DBI->connect("dbi:SQLite:dbname=alben.sqlite3", "", "", { RaiseError => 1, AutoCommit => 1 });
my $sql = SQL::Abstract->new();
my ($stmt, @bind) = $sql->select(
    ['artists artist', 'albums album', 'songs song'],
    ['artist.name', 'album.name', 'song.track', 'song.name', 'song.duration'],
    {
        'artist.id' => \[ '= album.artist_id' ],
        'album.id'  => \[ '= song.album_id'   ],
        'song.name' =>  { like => $search     },
    }
);
my @rows = $dbh->selectall_array($stmt, {}, @bind);

# Generate JSON
if ($json) {
    my $json = { Songs => [] };
    for my $row ( @rows ) {
        push @{$json->{Songs}}, {
            Artist  => $row->[0],
            Album   => $row->[1],
            TrackNo => $row->[2],
            Song    => $row->[3],
            Seconds => $row->[4],
        };
    }
    printf "%s\n", (JSON->new->utf8->pretty(1)->encode($json));
}
# Generate Table
else {
    my $table = Text::Table->new("Artist", "Album Name", "Track", "Song", "Duration");
    for my $row ( @rows ) {
        $table->add(@$row);
    };
    printf "%s\n", ($table->stringify);
}

$dbh->disconnect;
